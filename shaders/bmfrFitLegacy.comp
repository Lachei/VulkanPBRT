#version 460

#include "bmfrGeneral.comp" //includes all layout declarations and common functions

shared float uVec[PIXEL_BLOCK];
shared vec3 rMat[(ALPHA_SIZE - 2) * (ALPHA_SIZE - 1) / 2];
shared float uLengthSquared;
shared vec3 divider;
void main(){
    //note: this kernel always runs in a one dimensional block, so local_size_x,y = 1

    float prod = 0;

    const int groupId = int(gl_WorkGroupID.x * gl_NumWorkGroups.y + gl_WorkGroupID.y);
    const int id = int(gl_LocalInvocationIndex);
    const ivec2 basePixel = ivec2(gl_WorkGroupID.xy) * ivec2(PIXEL_BLOCK_WIDTH, PIXEL_BLOCK_WIDTH);

    // Computing R matrix
    for(int col = 0; col < int(ALPHA_SIZE); ++col){
        int colLimited = min(col, int(ALPHA_SIZE) - 3); // not needed as the last three columns are never processed
        //load column
        float tmpSum = 0;
        for(int subVector = 0; subVector < PIXEL_BLOCK / BLOCK_WIDTH; ++subVector){
            const int index = id + subVector * BLOCK_WIDTH;     //holds the linearized pixel index in the block
            const ivec2 pixelCoords = ivec2(index / PIXEL_BLOCK_WIDTH, index % PIXEL_BLOCK_WIDTH) + basePixel;
            float tmp = imageLoad(featureBuffer, ivec3(pixelCoords, col)).x;

            //if(isnan(tmp)) tmp = 1;
            tmp = clamp(tmp, -1e6, 1e6);
            uVec[index] = tmp;
            if(index > col){
                tmpSum += tmp * tmp;
            }
        }

        // Length of vecto in A's column with reduction sum
        float vecLength = parallel_reduction_sum(tmpSum);

        float rValue;
        if((id < col)){
            rValue = uVec[id];
        }
        else if(id == col){
            uLengthSquared = vecLength;
            vecLength = sqrt(vecLength + uVec[colLimited] * uVec[colLimited]);
            uVec[colLimited] -= vecLength;
            uLengthSquared += uVec[colLimited] * uVec[colLimited];
            // uLengthSquared is now updated length squared
            rValue = vecLength;
        }
        else if(id > col){
            // entries below the diagonal are always 0
            rValue = 0;
        }

        int idLimited = min(id, int(ALPHA_SIZE) - 3);
        int ind = rIndex(colLimited, idLimited);
        if(col < ALPHA_SIZE - 3){
            // broadcasting the value accross all entries
            rMat[ind] = vec3(rValue);
        }
        else{
            // storing only a single channel
            rMat[ind][col - int(ALPHA_SIZE) + 3] = rValue;
        }
        barrier();

        // Transformation of the columns to the right of the current column
        // Note: The last 3 columns are the color channels, which
        // are also transformed
        for(int feature = colLimited + 1; feature < ALPHA_SIZE; ++feature){
            // Used to cache the entries for each column
            float tmpDataCache[PIXEL_BLOCK / BLOCK_WIDTH];

            float tmpSum = .0f;
            for(int subVector = 0; subVector < PIXEL_BLOCK / BLOCK_WIDTH; ++subVector){
                const int index = id + subVector * BLOCK_WIDTH;
                if(index >= colLimited){
                    const ivec2 pixelCoords = ivec2(index / PIXEL_BLOCK_WIDTH, index % PIXEL_BLOCK_WIDTH) + basePixel;
                    float tmp = imageLoad(featureBuffer, ivec3(pixelCoords, feature)).x;

                    // adding noise the first time values are loaded
                    if(col == 0 && feature < ALPHA_SIZE - 3){
                        tmp = addRandom(tmp, id, subVector, feature, int(camParams.frameNumber));
                    }

                    tmpDataCache[subVector] = tmp;
                    
                    tmpSum += tmp * uVec[index];
                }
            }

            float dotProd = parallel_reduction_sum(tmpSum);

            for(int subVector = 0; subVector < PIXEL_BLOCK / BLOCK_WIDTH; ++subVector){
                const int index = id + subVector * BLOCK_WIDTH;
                if(index >= colLimited){
                    float storeValue = tmpDataCache[subVector];

                    storeValue -= 2 * uVec[index] * dotProd / uLengthSquared;
                    const ivec2 pixelCoords = ivec2(index / PIXEL_BLOCK_WIDTH, index % PIXEL_BLOCK_WIDTH) + basePixel;
                    imageStore(featureBuffer, ivec3(pixelCoords, feature), vec4(storeValue));
                }
            }
            barrier();
        }
    }

    // Back substitution
    for(int i = R_EDGE - 2; i >= 0; --i){
        if(id == 0){
            int index = rIndex(i, i);
            divider = rMat[index];
        }
        barrier();
        if(id < R_EDGE && id >= i){
            int index = rIndex(id, i);
            vec3 value = rMat[index];
            value = value / divider;
            //if(isnan(value.x)) value = vec3(0);
            rMat[index] = value;
        }
        barrier();
        if(id == 0){
            for(int j = i + 1; j < R_EDGE - 1; ++j){
                int index = rIndex(R_EDGE - 1, i);
                int index2 = rIndex(j, i);
                rMat[index] = rMat[index] - rMat[index2];
            }
        }
        barrier();
        if(id < R_EDGE && i >= id){
            int index = rIndex(i, id);
            int index2 = rIndex(R_EDGE - 1, i);
            rMat[index] = rMat[index] * rMat[index2];
        }
        barrier();
    }

    // store weights
    if(id < ALPHA_SIZE - 3){
        int index = rIndex(R_EDGE - 1, id);
        const vec3 weight = rMat[index]; // load from r matrix
        
        imageStore(weights, ivec3(gl_WorkGroupID.xy, id * 3), vec4(weight.x));
        imageStore(weights, ivec3(gl_WorkGroupID.xy, id * 3 + 1), vec4(weight.y));
        imageStore(weights, ivec3(gl_WorkGroupID.xy, id * 3 + 2), vec4(weight.z));
    }
}