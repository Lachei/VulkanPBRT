#version 460
#extension GL_EXT_ray_tracing : enable
#extension GL_GOOGLE_include_directive : enable

#pragma import_defines (FINAL_IMAGE, FINAL_IMAGE_HQ, GBUFFER, PREV_GBUFFER, DEMOD_ILLUMINATION, DEMOD_ILLUMINATION_SQUARED)

#include "ptStructures.glsl"
#include "layoutPTAccel.glsl"
#include "layoutPTImages.glsl"
#include "layoutPTLights.glsl"
#include "layoutPTUniform.glsl"
#include "layoutPTPushConstants.glsl"

layout(location = 0) rayPayloadEXT bool shadowed;
layout(location = 1) rayPayloadEXT RayPayload rayPayload;
uint rayFlags = gl_RayFlagsNoOpaqueEXT | gl_RayFlagsOpaqueEXT;
uint cullMask = 0xff;
float tmin = 0.001;
float tmax = 10000.0;

#include "camera.glsl"
#include "lighting.glsl"

void main(){
    // --------------------------------------------------------------------
	// random engine generation + ray generation (including first hit infos and first hit direct lighting)
	// --------------------------------------------------------------------
    RandomEngine re = rEInit(gl_LaunchIDEXT.xy, camParams.frameNumber);
    vec3 throughput = vec3(1);
    vec4 worldSpacePos, worldSpaceDir;
    bool antiAlias = false;
    #ifdef FINAL_IMAGE
    #ifndef DEMOD_ILLUMINATION
    #ifndef DEMOD_ILLUMINATION_SQUARED
	if(camParams.sampleNumber > 0)
    	antiAlias = true;	//can not be done when reprojecting, never is done for first sample
    #endif
    #endif
    #endif
    createRay(gl_LaunchIDEXT.xy, gl_LaunchSizeEXT.xy, antiAlias, re, worldSpacePos, worldSpaceDir);
    traceRayEXT(tlas, rayFlags, cullMask, 0, 0, 0, worldSpacePos.xyz, tmin, worldSpaceDir.xyz, tmax, 1);
    vec3 finalColor = vec3(0);
    finalColor += nextEventEsitmation(rayPayload.position, -normalize(worldSpaceDir.xyz), rayPayload.si, throughput, re);
    finalColor += rayPayload.si.emissiveColor;
    // --------------------------------------------------------------------
	// storing GBuffer information
	// --------------------------------------------------------------------
	vec3 curAlbedo = (rayPayload.si.diffuseColor + rayPayload.si.specularColor).xyz;
	float depth = distance(rayPayload.position, worldSpacePos.xyz);
	vec3 curNorm = rayPayload.si.normal;
#ifdef GBUFFER
	imageStore(depthImage, ivec2(gl_LaunchIDEXT.xy), vec4(depth));
	vec2 compressedNormal;
	compressedNormal.x = acos(rayPayload.si.normal.z);
	compressedNormal.y = atan(rayPayload.si.normal.y, rayPayload.si.normal.x);
	imageStore(normalImage, ivec2(gl_LaunchIDEXT.xy), vec4(compressedNormal, 1, 1));
	float category_id = float(rayPayload.category_id) / 255.0;
	imageStore(materialImage, ivec2(gl_LaunchIDEXT.xy), vec4(category_id, 0, 0, 0));
	imageStore(albedoImage, ivec2(gl_LaunchIDEXT.xy), vec4(curAlbedo, 1));
#endif

    // --------------------------------------------------------------------
	// accumulation information
	// --------------------------------------------------------------------
 	vec3 prevColor;
	vec3 prevColorSquared;
	float truePrevDepth;
	bool reprojected = false;
	float pixelSpp = 1.0 / 256.0; //has to be normalized as only floating point 8 bit interp is supported
#if defined PREV_GBUFFER && defined GBUFFER && (defined DEMOD_ILLUMINATION || defined DEMOD_ILLUMINATION_SQUARED)
	mat4 prevProj = inverse(camParams.inverseProjectionMatrix);
	vec4 p = vec4(rayPayload.position, 1);
	vec4 prevPos = camParams.prevView * p;
	float preDepth = length(prevPos.xyz);
	prevPos = prevProj * prevPos;
	prevPos /= prevPos.w; // xy now holds the prev image position
	prevPos.xy += 1;
	prevPos.xy *= .5;
	prevPos.xy *= vec2(gl_LaunchSizeEXT.xy) / vec2(gl_LaunchSizeEXT.xy - .5);
	ivec2 prevPixelIndex = ivec2(prevPos.xy * (gl_LaunchSizeEXT.xy - vec2(1)) + .5f);
	if(isinf(rayPayload.position.x)) pixelSpp = 0;
	if(camParams.frameNumber > 0 && !isinf(rayPayload.position.x)){
		//depth check
		truePrevDepth = texture(prevDepth, prevPos.xy).x;
		float depthDissim = (truePrevDepth / preDepth) - 1;	//using relative error of depths to blend pixels further away form camera together
		if(all(greaterThanEqual(prevPos.xy, vec2(0))) && all(lessThanEqual(prevPos.xy, vec2(1))) && abs(depthDissim) <= .05f){		//dissimilarity in depth values shoudl be smaller than 1%
			//normal check
			vec3 prevNor;
			prevNor.xy = texture(prevNormal, prevPos.xy).xy;
			prevNor = vec3(sin(prevNor.x) * cos(prevNor.y), sin(prevNor.x) * sin(prevNor.y), cos(prevNor.x));
			if(abs(dot(rayPayload.si.normal, prevNor)) > .1f){	//we do have a point which can be reprojected
				reprojected = true;
			#ifdef DEMOD_ILLUMINATION
				prevColor = texture(prevOutput, prevPos.xy).xyz;
			#endif
			#ifdef DEMOD_ILLUMINATION_SQUARED
				prevColorSquared = texture(prevIlluminationSquared, prevPos.xy).xyz;
			#endif
				pixelSpp += texture(prevSampleCounts, prevPos.xy).x;
			}
		}
	}
	if(reprojected){
		imageStore(motion, ivec2(gl_LaunchIDEXT.xy), vec4(prevPos.xy, 1, 1));
	}
	else{
		imageStore(motion, ivec2(gl_LaunchIDEXT.xy), vec4(-1));
	}
	imageStore(sampleCounts, ivec2(gl_LaunchIDEXT.xy), vec4(pixelSpp));
#endif

    // --------------------------------------------------------------------
	//depth recursion
	// --------------------------------------------------------------------
#if defined FINAL_IMAGE || defined DEMOD_ILLUMINATION || defined DEMOD_ILLUMINATION_SQUARED
	if(rayPayload.si.normal != vec3(1)){
		for(int i = 0; i < infos.maxRecursionDepth; ++i){
			vec3 v = normalize(worldSpacePos.xyz - rayPayload.position);
			worldSpacePos = vec4(rayPayload.position, 1);
			vec3 indir = indirectLighting(worldSpacePos.xyz, v, rayPayload.si, i, throughput, re);
			if(rayPayload.si.normal == vec3(1)) break;	//stop recursion when sky was hit
			finalColor += indir;
		}
	}
#endif

    // --------------------------------------------------------------------
	// final color calculations
	// --------------------------------------------------------------------
	finalColor = clamp(finalColor, vec3(0), vec3(c_MaxRadiance));
#if defined DEMOD_ILLUMINATION || defined DEMOD_ILLUMINATION_SQUARED
	vec3 demodulated = finalColor;
	if(!isinf(rayPayload.position.x))
		demodulated = min(finalColor / (curAlbedo + vec3(EPSILON)), vec3(1e3));	// epsilon added to avoid divion by 0 (when color is remodulated, epsilon is added to albedo)
	vec3 demodSquared = demodulated * demodulated;
	if(reprojected){
		float blendAlpha = max(1.f / (pixelSpp * 256.0), BLEND_ALPHA);
		demodulated = mix(prevColor, demodulated, blendAlpha);
		demodSquared = mix(prevColorSquared, demodSquared, blendAlpha);
	}
	#ifdef DEMOD_ILLUMINATION
	imageStore(illumination, ivec2(gl_LaunchIDEXT.xy), vec4(demodulated, 1));
	#endif
	#ifdef DEMOD_ILLUMINATION_SQUARED
	imageStore(illuminationSquared, ivec2(gl_LaunchIDEXT.xy), vec4(demodSquared, 1));
	#endif
#endif

#ifdef FINAL_IMAGE
	vec3 prevFrameColor = vec3(1.0/0);
	if(camParams.sampleNumber > 0){
		prevFrameColor = imageLoad(outputImage, ivec2(gl_LaunchIDEXT.xy)).xyz;
		float alpha = 1.0 / (camParams.sampleNumber + 1);
		finalColor = mix(SRGBtoLINEAR(vec4(prevFrameColor,1)).xyz, finalColor, alpha);
	}

	finalColor = LINEARtoSRGB(vec4(finalColor, 1)).xyz;
	finalColor = clamp(finalColor, vec3(0), vec3(1));
	
	imageStore(outputImage, ivec2(gl_LaunchIDEXT.xy), vec4(finalColor, 1));
#endif
}